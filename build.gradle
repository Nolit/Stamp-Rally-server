apply plugin: 'war'

repositories {
    mavenCentral()
}
sourceCompatibility = '1.7'
targetCompatibility = '1.7'
dependencies {
    compile 'javax:javaee-api:7.0'
    compile 'org.eclipse.persistence:eclipselink:2.5.1'
    compile 'com.fasterxml.jackson.core:jackson-core:2.3.+'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.3.+'
    compile 'commons-codec:commons-codec:1.10'
    providedCompile 'javax.websocket:javax.websocket-api:1.1'
}

war {
    baseName = 'stamp-rally'
}

ext {
    asadmin = glassFishHome + (isWindows() ? '/bin/asadmin.bat'
                                           : '/bin/asadmin')
    //大脇用
    //asadmin = '/Users/air/glassfish_server/bin/asadmin'
    domain = 'domain1'
}

def isWindows() {
    return System.properties['os.name'].toLowerCase().contains('windows')
}

task startServer(type: Exec) {
    commandLine asadmin, 'start-domain', '--debug=true', domain
}

task stopServer(type: Exec) {
    commandLine asadmin, 'stop-domain', domain
}

task deploy(type: Exec, dependsOn: 'war') {
    commandLine asadmin, 'deploy', '--force=true', war.archivePath
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}